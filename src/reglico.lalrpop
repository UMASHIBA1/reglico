grammar;
use super::ast::{Ident, Opcode, Types, Expr, BlockExpr, ExprStmt, CallExpr, Operation, VariableDeclaration, FuncArg,  Func, ReturnStmt, IfStmt, BlockBox, CanElseStmt, Stmt};

pub Program = {
    Stmt*
}

Stmt: Stmt = {
    ConstAssignment => Stmt::VariableDeclaration(<>),
    ExprStmt => Stmt::ExprStmt(<>),
    ReturnStmt => Stmt::ReturnStmt(<>),
    Func => Stmt::Func(<>),
    IfStmt => Stmt::IfStmt(<>),
}

ExprStmt: ExprStmt = {
    <expr: Expr> ";" => ExprStmt::new(expr),
}

ConstAssignment: VariableDeclaration = {
    "const" <ident: IdentExpr> "=" <value: Expr> ";" => VariableDeclaration::new(ident, None, Some(value)),
    "const" <ident: IdentExpr> ":" <type_name: Types> "=" <value: Expr> ";" =>  VariableDeclaration::new(ident, Some(type_name), Some(value)),
}

IdentExpr: Ident = {
    r"[a-zA-Z_][a-zA-Z0-9_]*" => Ident::new(<>.to_string())
}

Types: Types = {
    "number" => Types::NumberType,
    "bool" => Types::BoolType,
}

Expr: Expr = {
    <left: Expr> <op: ComparisonOpcode> <right: Arithmetic> => Expr::Op(Operation::new(left, op, right)),
    Arithmetic,
}

Arithmetic: Expr = {
    <left: Arithmetic> <op: ArithmeticOpcode> <right: Factor> => Expr::op_new(left, op, right),
    Factor
}

Factor: Expr = {
    <left: Factor> <op: FactorOpcode> <right: Term> => Expr::op_new(left, op, right),
    Term
}

ComparisonOpcode: Opcode = {
    "<=" => Opcode::LessThanOrEqual,
}

ArithmeticOpcode: Opcode = {
    "+" => Opcode::Add,
    "-" => Opcode::Sub,
}

FactorOpcode: Opcode = {
    "*" => Opcode::Mul,
    "/" => Opcode::Div,
}

Term: Expr = {
    Num,
    Bool => Expr::bool_new(<>),
    IdentExpr => Expr::ident_new(<>),
    CallExpr => Expr::Call(<>),
    BlockExpr => Expr::Block(<>),
    "(" <Expr> ")",
}

Num: Expr = {
   r"[0-9]+" => Expr::num_new(<>.parse::<i64>().unwrap(), <>),
}

Bool: bool = {
    "true" => true,
    "false" => false,
}

CallExpr: CallExpr = {
    <ident: IdentExpr> "(" <args: CommaSeparate<Expr>> ")" => CallExpr::new(ident, args)
}

BlockExpr: BlockExpr = {
    "{" <stmts: Stmt*> "}" => BlockExpr::new(stmts)
}

Func: Func = {
    "fn" <name: IdentExpr> "(" <args: CommaSeparate<FuncArg>> ")" "{" <stmts: Stmt*> "}" => Func::new(name, args, stmts)
}

FuncArg: FuncArg = {
    <ident: IdentExpr> ":" <arg_type: Types> => FuncArg::new(ident, arg_type),
}

ReturnStmt: ReturnStmt = {
    "return" <expr: Expr> ";" => ReturnStmt::new(expr),
}

IfStmt: IfStmt = {
    "if" <expr: Expr> <then_stmt: BlockBox> "else" <else_stmt: BlockBox> => IfStmt::new(expr, then_stmt, Some(CanElseStmt::new_from_block_box(else_stmt))),
    "if" <expr: Expr> <then_stmt: BlockBox> "else" <else_stmt: IfStmt> => IfStmt::new(expr, then_stmt, Some(CanElseStmt::new_from_if_stmt(else_stmt))),
    "if" <expr: Expr> <then_stmt: BlockBox> => IfStmt::new(expr, then_stmt, None),
}

BlockBox: BlockBox = {
    "{" <stmts: Stmt*> "}" => BlockBox::new(stmts),
}

CommaSeparate<T>: Vec<T> = { // (1)
    <mut v:(<T> ",")*> <e:T?> => match e { // (2)
        None => v,
        Some(e) => {
            v.push(e);
            v
        }
    }
};
